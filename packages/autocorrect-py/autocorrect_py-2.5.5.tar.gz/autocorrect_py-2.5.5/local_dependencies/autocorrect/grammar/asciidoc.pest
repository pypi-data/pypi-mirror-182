
// https://docs.asciidoctor.org/asciidoc/latest
item = _{ SOI ~ line* ~ EOI }
line = _{ expr | newline }
expr = _{ block | inline }

block = ${ heading | codeblock | td_tag | paragraph }
inline = _{ img | link | code | mark }

string = ${ (!(inline | newline) ~ ANY)+ }
paragraph = { (string | inline)+ }

newline = ${ "\n" | "\r" }
space = @{ (" ")* }

codeblock = ${ 
  codeblock_filename* ~ codeblock_meta* ~ 
  PUSH("----" | "....") ~ codeblock_lang ~ codeblock_code ~ PUSH("----" | "....")
}
codeblock_filename = @{ "." ~ (!newline ~ ANY)+ ~ newline }
codeblock_meta = @{ "[" ~ (!"]" ~ ANY)+ ~ "]" ~ newline  }
codeblock_lang = { ASCII_ALPHA* }
codeblock_code = { (!(PEEK) ~ ANY)* }

td_tag = @{ space ~ "|" ~ space }


img_start = @{ "!" }
img = ${ img_start ~ link }

link = ${ link_string ~ href }
link_string = { "[" ~ (!("]" | newline) ~ ANY)* ~ "]" } 
href = { "(" ~ (!(")" | newline) ~ ANY)* ~ ")" }

open_mark = @{
  // Bold https://docs.asciidoctor.org/asciidoc/latest/text/bold/
  "**"
  // https://docs.asciidoctor.org/asciidoc/latest/text/bold/
  | "*"
  // Italic https://docs.asciidoctor.org/asciidoc/latest/text/italic/
  | "_"
  // Highlight https://docs.asciidoctor.org/asciidoc/latest/text/highlight/
  | "#"
  // Subscript https://docs.asciidoctor.org/asciidoc/latest/text/subscript-and-superscript/
  | "~"
  // Superscript
  | "^"
}
close_mark = @{ POP }
mark = ${ PUSH(open_mark) ~ (mark | mark_string) ~ close_mark }
mark_string = { (!(PEEK | newline | inline) ~ ANY)* }

code = ${ PUSH(open_code) ~ inner_code ~ close_code }
open_code = @{ "`" }
close_code = @{ POP }
inner_code = @{ (!(newline | PEEK) ~ ANY)*  }

heading_tag = @{ ("======" | "=====" | "====" | "===" | "==" | "=") ~ " "* }
heading = ${ PUSH(heading_tag) ~ string }
